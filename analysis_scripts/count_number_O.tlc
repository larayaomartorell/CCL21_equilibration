# 
# Use with: vmd -dispdev text -e <nameofscript.tcl>
# By Jordi Faraudo March 2017

#
# PROCEDURE 1: BIG DCD
#First of all I include here the BigDCD command
#

proc bigdcd { script type args } {
    global bigdcd_frame bigdcd_proc bigdcd_firstframe vmd_frame bigdcd_running
  
    set bigdcd_running 1
    set bigdcd_frame 0
    set bigdcd_firstframe [molinfo top get numframes]
    set bigdcd_proc $script

    # backwards "compatibility". type flag is omitted.
    if {[file exists $type]} { 
        set args [linsert $args 0 $type] 
        set type auto
    }
  
    uplevel #0 trace variable vmd_frame w bigdcd_callback
    foreach dcd $args {
        if { $type == "auto" } {
            mol addfile $dcd waitfor 0
        } else {
            mol addfile $dcd type $type waitfor 0
        }
    }
    after idle bigdcd_wait
}

proc bigdcd_callback { tracedvar mol op } {
    global bigdcd_frame bigdcd_proc bigdcd_firstframe vmd_frame
    set msg {}
 
    # If we're out of frames, we're also done 
    # AK: (can this happen at all these days???). XXX
    set thisframe $vmd_frame($mol)
    if { $thisframe < $bigdcd_firstframe } {
        puts "end of frames"
        bigdcd_done
        return
    }
 
    incr bigdcd_frame
    if { [catch {uplevel #0 $bigdcd_proc $bigdcd_frame} msg] } { 
        puts stderr "bigdcd aborting at frame $bigdcd_frame\n$msg"
        bigdcd_done
        return
    }
    animate delete beg $thisframe end $thisframe $mol
    return $msg
}

proc bigdcd_done { } {
    global bigdcd_running
    
    if {$bigdcd_running > 0} then {
        uplevel #0 trace vdelete vmd_frame w bigdcd_callback
        puts "bigdcd_done"
        set bigdcd_running 0
    }
}

proc bigdcd_wait { } {
    global bigdcd_running bigdcd_frame
    while {$bigdcd_running > 0} {
        global bigdcd_oldframe
        set bigdcd_oldframe $bigdcd_frame
        # run global processing hooks (including loading of scheduled frames)
        display update ui
        # if we have read a new frame during then the two should be different.
        if { $bigdcd_oldframe == $bigdcd_frame } {bigdcd_done}
    }
}

#
# Define procedure for computing OC - Ytrium bonds
#

proc X_Y_bonds { frame } {
  global seleccio fp
  puts "$frame"
  $seleccio frame $frame
  $seleccio update 
  set n [$seleccio num] 
  puts $fp "$frame $n" 
}

#
#Main program
#
# output file MODIFY THIS NAME
set fp [ open "contacts_O.txt" w ] 

# open structure file of simulated system MODIFY THIS
set mol [mol new ../input/ionized.psf type psf waitfor all]

# define the interesting thing to calculate SAME SELECTION AS IN VMD NOW INSIDE THE {}
set seleccio [atomselect $mol {resname PEG and type OG301 and within 3.5 of protein}]

#perform calculation over each frame using BigDCD
puts "Please wait. Calculating..."
bigdcd X_Y_bonds auto ../output3/equilNpT_stride10.dcd
bigdcd_wait

#close output file          
close $fp 

exit

